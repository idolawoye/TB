rule all:
    input:
        auspice_tree = "auspice/tb2_tree.json",
        auspice_meta = "auspice/tb2_meta.json"

# Config variables to be used by rules
# Parameters are defined within their own rules

rule config:
    params:
        seq = "data/core.vcf",
        meta = "data/tbgermany_meta.csv",
        exclude = "data/dropped_strains.txt",
        ref = "data/ref.fa",
        generef = "data/Mtb_H37Rv_NCBI_Annot.gff",
        sites = "data/drm_sites.txt",
        mask = "data/Locus_to_exclude_Mtb.bed",
        colors = "data/colour.txt",
        genes = "data/genes.txt",
        config = "data/config.json",
        geo_info = "data/lats_longs.txt",
        drms = "data/DRMs-AAnuc.tsv"

config = rules.config.params #so we can use config.x rather than rules.config.params.x
#end of config definition

rule filter:
    input:
        seq = config.seq,
        meta = config.meta
    output:
        "results/filtered.vcf.gz"
    params:
        sequences_per_group = 10,
        group_by = "year month",
        min_len = 200000
    shell:
        """
        augur filter --sequences {input.seq} --metadata {input.meta} \
            --output {output} \
            --group-by {params.group_by} \
            --sequences-per-group {params.sequences_per_group} \
        """

rule mask:
    input:
        seq = rules.filter.output,
        mask = config.mask
    output:
       "results/masked.vcf.gz"
    shell:
        "augur mask --sequences {input.seq} --output {output} --mask {input.mask}"

rule tree:
    input:
        aln = rules.mask.output,
        ref = config.ref,
        sites = config.sites
    output:
        "results/tree_raw.nwk"
    params:
        method = 'iqtree'
    shell:
        'augur tree --alignment {input.aln} --vcf-reference {input.ref} --output {output} --method {params.method}'

rule refine:
    input:
        tree = rules.tree.output,
        aln = rules.mask.output,
        metadata = config.meta,
        ref = config.ref
    output:
        tree = "results/masked.vcf_tt.nwk",
        node_data = "results/branch_lengths.json",
    params:
        root = 'min_dev',
        coal = 'opt'
    shell:
        """
        augur refine --tree {input.tree} \
        	--alignment {input.aln} \
        	--vcf-reference {input.ref} \
        	--metadata {input.metadata} \
        	--timetree \
        	--root {params.root} \
        	--coalescent {params.coal} \
        	--output-node-data {output.node_data}
        """

rule ancestral:
    input:
        tree = rules.refine.output.tree,
        alignment = config.seq,
        ref = config.ref
    output:
        nt_data = "results/nt_muts.json",
        vcf_out = "results/nt_muts.vcf"
    params:
        inference = "joint"
    shell:
        """
        augur ancestral --tree {input.tree} --alignment {input.alignment} \
            --output {output.nt_data} --inference {params.inference} \
            --output-vcf {output.vcf_out} --vcf-reference {input.ref}

        """
rule translate:
    input:
        tree = rules.refine.output.tree,
        ref = config.ref,
        gene_ref = config.generef,
        vcf = rules.ancestral.output.vcf_out,
        genes = config.genes
    output:
        aa_data = "results/aa_muts.json",
        vcf_out = "results/translations.vcf",
        vcf_ref = "results/translations_reference.fasta"
    shell:
        """
        augur translate --tree {input.tree} \
        --vcf-reference {input.ref} \
        --ancestral-sequences {input.vcf} \
        --genes {input.genes} \
        --reference-sequence {input.gene_ref} \
        --output {output.aa_data} \
        --alignment-output {output.vcf_out} \
        --vcf-reference-output {output.vcf_ref}
        """

rule traits:
    input:
        tree = rules.refine.output.tree,
        meta = config.meta
    output:
        "results/traits.json"
    params:
        traits = 'location location'
    shell:
        'augur traits --tree {input.tree} --metadata {input.meta}'
        ' --columns {params.traits} --output {output}'

rule seqtraits:
    input:
        align = rules.ancestral.output.vcf_out,
        ref = config.ref,
        trans_align = rules.translate.output.vcf_out,
        trans_ref = rules.translate.output.vcf_ref,
        drms = config.drms
    output:
        drm_data = "results/drms.json"
    params:
        count = "traits",
        label = "Drug_resistance"
    shell:
        """
        augur sequence-traits \
        --ancestral-sequences {input.align} \
        --vcf-reference {input.ref} \
        --translations {input.trans_align} \
        --vcf-translate-reference {input.trans_ref} \
        --features {input.drms} \
        --count {params.count} \
        --label {params.label} \
        --output {output.drm_data}
        """

rule export:
    input:
        tree = rules.refine.output.tree,
        metadata = config.meta,
        branch_lengths = rules.refine.output.node_data,
        traits = rules.traits.output,
        nt_muts = rules.ancestral.output.nt_data,
        aa_muts = rules.translate.output.aa_data,
        drms = rules.seqtraits.output.drm_data,
        color_defs = config.colors,
        config = config.config,
        geo_info = config.geo_info
    output:
        tree = rules.all.input.auspice_tree,
        meta = rules.all.input.auspice_meta
    shell:
        'augur export --tree {input.tree} --metadata {input.metadata}'
            ' --node-data {input.branch_lengths} {input.traits} {input.nt_muts} {input.aa_muts} {input.drms}'
            ' --auspice-config {input.config} --colors {input.color_defs} --output-tree {output.tree}'
            ' --lat-longs {input.geo_info} --output-meta {output.meta}'
